plugins {
    id 'java'
    id 'org.javamodularity.moduleplugin' version "1.8.12"
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
//    id 'org.graalvm.buildtools.native' version '0.9.23'
}

apply plugin: 'java'

version '0.0.1-beta'
project.description = "Zenmart ERP - Fintech Software."
project.ext.buildDate = new Date()
project.version = "1.0.0"

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.infinite.spoty'
    mainClass = 'org.infinite.spoty.Main'
}

javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.media', 'javafx.graphics']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

//graalvmNative {
//    toolchainDetection = true
//    binaries {
//        main {
//            javaLauncher = javaToolchains.launcherFor {
//                languageVersion = JavaLanguageVersion.of(17)
//                vendor = JvmVendorSpec.matching("Oracle Corporation")
//            }
//        }
//    }
//}

test {
    useJUnitPlatform()
}

run {
    jvmArgs = [
            '-Djdk.gtk.version=3',
            '-XX:NewRatio=1',
            '-Xms256m',
            '-Xmx2G',
            '-XX:ReservedCodeCacheSize=512m',
            '-XX:+UseG1GC',
            '-XX:SoftRefLRUPolicyMSPerMB=50',
            '-XX:CICompilerCount=2',
            '-XX:+HeapDumpOnOutOfMemoryError',
            '-XX:-OmitStackTraceInFastThrow',
            '-XX:+IgnoreUnrecognizedVMOptions',
            '-Dsun.io.useCanonCaches=false',
            '-Dsun.java2d.metal=true',
            '-Djbr.catch.SIGABRT=true',
            '-Djdk.http.auth.tunneling.disabledSchemes=""',
            '-Djdk.attach.allowAttachSelf=true',
            '-Djdk.module.illegalAccess.silent=true'
    ]
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.infinite.spoty.Main'
        )
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Zenmart ERP'
        jvmArgs = [
                '-Djdk.gtk.version=3'
        ]
    }

    jpackage {
        // Could be taken from command line, here it is defined statically
        // project.findProperty('installerOs')
        //    (example: -PinstallerOs=mac)
        targetPlatformName = 'linux-x64'
        // Resource directory for native package overrides,
        // you can do lots of magic here too...
        // resourceDir = file('package/')

        if (targetPlatformName == 'mac') {
            targetPlatform("mac") {
                jdkHome = '/Library/Java/JavaVirtualMachines/temurin-17.jdk/Contents/Home'
            }
            installerType = 'pkg'
        }
        if (targetPlatformName == 'win') {
            targetPlatform("win") {
                jdkHome = 'C:/Applications/JDKs/Adoptium/temurin-17.jdk'
            }
            installerType = 'exe'
        }
        if (targetPlatformName == 'linux-x64') {
            targetPlatform("linux-x64") {
                addExtraModulePath("/home/infinite/Java_FX/javafx-jmods-20/linux_x64")
            }
            installerType = 'deb'
        }

        installerOptions = [
                '--name', 'Zenmart-ERP',
                '--description', project.description,
                '--copyright', 'Copyrigth 2023 infinite Studios',
                '--vendor', 'infinite Studios'
        ]

        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/icon.icns']
            installerOptions += [
                    '--license-file', 'package/LICENSE-OS-Installer.txt'
            ]
        }
        if (installerType == 'exe') {
            imageOptions += ['--icon', 'src/main/resources/icon.ico']
            installerOptions += [
                    // '--win-per-user-install',
                    // '--win-console',
                    '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'src/main/resources/logo.png']
            installerOptions += [
                    '--linux-menu-group', 'Work',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'info@codecellar.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                    '--linux-rpm-license-type', 'GPLv3'
            ]
        }
    }
    addExtraDependencies('javafx')
}

tasks.register('doPackageAll') {
    doLast {
        if (Os.isFamily(Os.FAMILY_UNIX)) {
            exec {
                mkdir("$buildDir/distributions")
                executable "$projectDir/scripts/JLinkPackage.sh"
                args = ["$buildDir"]
            }
        }
    }
}

jlinkZip.doLast {
    doPackageAll
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.43.0.0'
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'io.github.palexdev:materialfx:11.16.1'
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation "org.kordamp.ikonli:ikonli-core:12.3.1"
    implementation "org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1"
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:4.0.1'
    implementation "fr.brouillard.oss:cssfx:11.5.1"
    implementation "io.github.palexdev:scenicview:17.0.2"
    implementation "io.github.palexdev:virtualizedfx:11.9.6"
    implementation "io.github.palexdev:mfxcomponents:11.22.0"
    implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}
